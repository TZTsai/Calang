LINE    := %SEQ < ( CMD | DEF | EXP | EMPTY ) ; >
EMPTY   := /\s*/

CMD     := CONF | DIR | DEL | LOAD | IMPORT
CONF    := config /\w+/ ( NUM | /on|off/ ) ?
DIR     := dir FIELD ?
DEL     := del %SEQ < FIELD , >  ## DELAY
LOAD    := load /[\w\.]+/ /-[tvw]/ *
IMPORT  := import /[\w\.]+/ /-[vw]/ *

DEF     := SUPER:FIELD ? BIND
SUPER   := ( NAME . ) +
BIND    := ( FUNC | _VAR ) AT ? = EXP DOC ?  ## DELAY
FUNC    := NAME FORM
_VAR    := VARS | NAME
VARS    := %BKT < %SEQ < _VAR , > >
DOC     := /".*"/

FIELD   := NAME ATTR *
NAME    := /[\u0374-\u03FFa-zA-Z][a-zA-Z\d_]*[?]?/    ## can begin with a Greek letter
ATTR    := . / NAME:ATTR                              ## '/' forces no space in between

FORM    := PAR_LST | NAME:PAR
PAR_LST := %BKT < ( _PARS ( , _OPTS ) ? ( , _EXT ) ? |  _OPTS ( , _EXT ) ? | _EXT ? ) >
_PARS   := %SEQ < ( BIND ! FORM ~ ! ) , >             ## '!' forbids the match
_OPTS   := %SEQ < ( BIND:OPTPAR ~ ! ) , >
_EXT    := NAME:EXTPAR ~

EXP     := MAP | CLOSURE | ENV | BODY

MAP     := FORM => EXP:DELAY
CLOSURE := ENV EXP:DELAY
ENV     := AT | DICT | MATCH
AT      := @ _VAL
DICT    := %GRP < %SEQ < BIND , > >
MATCH   := FORM :: _VAL
_VAL    := APPLY:SEQ | ITEM
APPLY   := FIELD ( GEN_LST | VAL_LST )

BODY    := PRINT ? ( IF_ELSE | SEQ ) PRINT ?
IF_ELSE := SEQ if SEQ else EXP  ## DELAY
SEQ     := _TERM ( BOP ? _TERM ) *
_TERM   := LOP ? ITEM ATTR ? ROP ?

PRINT   := /`.*?`/
ITEM    := GROUP | WHEN | ATOM | LIST
GROUP   := %GRP < EXP >
WHEN    := when %GRP < ( ( CASE , ) + EXP ) >  ## DELAY
CASE    := EXP : EXP

ATOM    := FIELD | SYM | ANS | NUM
SYM     := ' NAME:SYM
ANS     := /_(\d+|_*)/

NUM     := BIN | HEX | COMPLEX | REAL
COMPLEX := REAL [+-] REAL I
REAL    := /-?\d+(\.\d+)?/ ( [eE] - /-?\d+/ ) ?
BIN     := /0b[01]+/
HEX     := /0x[0-9a-fA-F]+/

LIST    := ' SYM_LST | GEN_LST | IDC_LST | VAL_LST
SYM_LST := %LST < ( UNQUOTE | _SYM_IT ) , >
UNQUOTE := ` ( LIST | NAME )  ## convert the item back to a value
_SYM_IT := SYM_LST | NAME:SYM
GEN_LST := %BKT < ( EXP ( for CONSTR ) + ) >  ## DELAY
CONSTR  := FORM in EXP ( if EXP ) ?
IDC_LST := %BKT < %SEQ < SLICE , > >
SLICE   := _SLC : _SLC ( : _SLC ) ?
_SLC    := ( SEQ | EMPTY )
VAL_LST := _DLST | %LST < EXP , >
_DLST   := %LST < SUB_LST:VAL_LST ; >
SUB_LST := %SEQ < EXP , > ?

### MACROS ###
%LST < $ITM $SEP >  := %BKT < ( %SEQ < $ITM $SEP > ? ) >
%BKT < $EXP >       := "[" - $EXP "]" -
%GRP < $EXP >       := "(" - $EXP ")" -
%SEQ < $ITM $SEP >  := $ITM ( $SEP $ITM ) *