LINE    := ( CMD | EVAL | EMPTY ) COMMENT ?

EMPTY   := /\s*/
COMMENT := # /.*/

CMD     := CONF | DIR | INFO | DEL | LOAD | IMPORT | PYTHON | EXIT

CONF    := config /\w+/ ( NUM | /on|off/ ) ?
DIR     := dir VAR ? "*" ?
DEL     := del @SEQ VAR ,
LOAD    := load /[\w\.]+/ /-[tvw]/ *
IMPORT  := import /[\w\.]+/ /-[vw]/ *
INFO    := info VAR ?
PYTHON  := %%    ## starts a python REPL session
EXIT    := exit

EVAL    := @SEQ EXPR ;
EXPR    := BIND | EXP
BIND    := NS ? FORM = EXPR DOC:COMMENT ?
NS      := @ ITEM    ## namespace
FORM    := VAR ITEM | PHRASE
VAR     := NAME ATTR *
NAME    := /[^\W\d][\w]*/
ATTR    := . / ATTR:NAME
EXP     := MAP | AT | PHRASE

MAP     := FORM:ITEM -> EXP
AT      := @ ITEM EXP
PHRASE  := ITEM +

ITEM    := ENV | GROUP | QUOTE | UNQUOTE | GENER | LIST | ARRAY | ATOM
ENV     := @GRP @SEQ BIND ,
GROUP   := @GRP EXP
QUOTE   := ' EXP '
UNQUOTE := { EXP }

GENER   := [ EXP @ ( @SEQ EXP , ) ]
LIST    := [ ( @SEQ LSITEM , ) ? ]
LSITEM  := KWD | EXP
KWD     := NAME = EXP
ARRAY   := [ ( @SEQ SUBARR ; ) ]
SUBARR  := /\t*/ - ( @SEQ EXP ( /\t+/ - ) ) /\t*/ -

ATOM    := STR | OP | ANS | ATTR | VAR | NUM | UNKNOWN
ANS     := /\$(\d+|\$*)/
STR     := /[a-z]?".*?"/
OP      := ???    ## define it in 'grammar.py'
UNKNOWN := /\?\w*/

NUM     := BIN | HEX | COMPLEX | REAL
COMPLEX := REAL /[+-]/ REAL I
REAL    := /-?\d+(\.\d+)?/ ( /[eE]/ - /-?\d+/ ) ?
BIN     := /0b[01]+/
HEX     := /0x[0-9a-fA-F]+/


## MACROS ##
@GRP $EXP       := "(" - $EXP ")" -
@SEQ $ITM $SEP  := $ITM ( $SEP $ITM ) *