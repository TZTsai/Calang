LINE    := @SEQ ( CMD | EXP | EMPTY ) ;
EMPTY   := /\s*/

CMD     := CONF | DIR | INFO | DEL | LOAD | IMPORT | EXIT
CONF    := config /\w+/ ( NUM | /on|off/ ) ?
DIR     := dir VAR ?
DEL     := del @SEQ VAR ,
LOAD    := load /[\w\.]+/ /-[tvw]/ *
IMPORT  := import /[\w\.]+/ /-[vw]/ *
INFO    := info VAR ?
EXIT    := exit

EXP     := BIND | MAP | CLOSURE | VAL
BIND    := VAR ARGS:ITEMS ? = EXP DOC:STR ?         ## DELAY this
MAP     := FORM:ITEMS -> EXP                        ## DELAY this
CLOSURE := @ ITEM EXP                               ## DELAY this

VAR     := NAME ATTR *
NAME    := /[^\W\d][\w]*'*/
ATTR    := . / ATTR:NAME

VAL     := IF_ELSE | ITEMS
IF_ELSE := ITEMS if SEQ else EXP                    ## DELAY this
SEQ     := @SEQ EXP ,
ITEMS   := ITEM +                                   ## sentence

ITEM    := GROUP | QUOTE | ATOM | LIST | PRINT
GROUP   := @GRP SEQ
QUOTE   := ' ITEM
PRINT   := /f`.*?`/
## UNQUOTE := $ ITEM                                ## TODO
## MACRO   := & / NAME ITEM                         ## TODO

ATOM    := VAR | UNKNOWN | ANS | NUM | STR
UNKNOWN := /\?\w*/
ANS     := /%(\d+|%*)/
STR     := /".*?"/

NUM     := BIN | HEX | COMPLEX | REAL
COMPLEX := REAL /[+-]/ REAL I
REAL    := /-?\d+(\.\d+)?/ ( /[eE]/ - /-?\d+/ ) ?
BIN     := /0b[01]+/
HEX     := /0x[0-9a-fA-F]+/

LIST    := [ SEQ ? ] | GEN_LST | MATRIX
GEN_LST := [ EXP "|" - CONSTR:SEQ ]                 ## DELAY this
MATRIX  := @LST SEQ ;


### MACROS ###
@LST $ITM $SEP  := [ ( @SEQ $ITM $SEP ) ? ]
@GRP $EXP       := "(" - $EXP ")" -
@SEQ $ITM $SEP  := $ITM ( $SEP $ITM ) *