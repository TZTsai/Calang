LINE    := CMD | EXPS | EMPTY

EMPTY   := /\s*/

CMD     := CONF | DIR | INFO | DEL | LOAD | IMPORT | INNER | EXIT

CONF    := config /\w+/ ( NUM | /on|off/ ) ?
DIR     := dir VAR ?
DEL     := del @SEQ VAR ,
LOAD    := load /[\w\.]+/ /-[tvw]/ *
IMPORT  := import /[\w\.]+/ /-[vw]/ *
INFO    := info VAR ?
INNER   := %%                                       ## start python REPL
EXIT    := exit

EXPS    := @SEQ EXP ;
EXP     := BIND | MAP | AT | PHRASE

BIND    := VAR ARG ? = EXP DOC:STR ?                ## DELAY
VAR     := NAME ATTR *
ARG     := NAME | LIST
NAME    := /[^\W\d][\w]*/
ATTR    := . / ATTR:NAME

MAP     := ARG -> EXP                               ## DELAY
AT      := @ ITEM EXP                               ## DELAY
PHRASE  := ITEM +                                   ## DELAY

ITEM    := ENV | GROUP | QUOTE | GENER | LIST | ARRAY | ATOM
ENV     := @GRP @SEQ BIND ,
GROUP   := @GRP EXP
QUOTE   := ' EXP '

GENER   := [ EXP @ ( @SEQ EXP , ) ]                 ## DELAY
LIST    := [ ( @SEQ EXP , ) ? ]
ARRAY   := [ ( @SEQ SUBARR ; ) ]
SUBARR  := /\t*/ - ( @SEQ EXP ( /\t+/ - ) ) /\t*/ -

ATOM    := STR | OP | ANS | ATTR | NAME | NUM | UNKNOWN
ANS     := /\$(\d+|\$*)/
STR     := /[prm]?".*?"/
OP      := <define-it-in-python-file>
UNKNOWN := /\?\w*/

NUM     := BIN | HEX | COMPLEX | REAL
COMPLEX := REAL /[+-]/ REAL I
REAL    := /-?\d+(\.\d+)?/ ( /[eE]/ - /-?\d+/ ) ?
BIN     := /0b[01]+/
HEX     := /0x[0-9a-fA-F]+/


### MACROS ###
@GRP $EXP       := "(" - $EXP ")" -
@SEQ $ITM $SEP  := $ITM ( $SEP $ITM ) *